/*
 * Copyright (c) 2020-2021. CodeHead Systems. All rights reserved
 * Ned Wolpert <ned.wolpert@codeheadsystems.com>
 */


ext {
    appName = "gamelib-entity"
}

configurations {
    project.archivesBaseName = "${appName}"
    jar.baseName = "${appName}"
}

jacocoTestReport.finalizedBy jacocoTestCoverageVerification

dependencies {
    implementation project(":gamelib-core")
    api "com.badlogicgames.gdx:gdx:$gdxVersion"
    api "com.badlogicgames.ashley:ashley:$ashleyVersion"

    // Dagger
    implementation "com.google.dagger:dagger:${daggerVersion}" // Common files for runtime
    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"

    // Immutables
    implementation("org.immutables:value-annotations:${immutablesVersion}")
    annotationProcessor("org.immutables:value:${immutablesVersion}")


    testImplementation("org.assertj:assertj-core:${assertjVersion}")
    testImplementation("org.junit.jupiter:junit-jupiter-engine:${jupiterVersion}")
    testImplementation("org.junit.jupiter:junit-jupiter-api:${jupiterVersion}")
    testImplementation("org.junit.jupiter:junit-jupiter-params:${jupiterVersion}")
    testImplementation("org.mockito:mockito-junit-jupiter:${mockitoVersion}")
    testImplementation("org.mockito:mockito-core:${mockitoVersion}")
    testImplementation("com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion")
    testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = appName
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = appName
                description = 'Entity system support for libGDX with dagger support.'
                url = "https://github.com/wolpert/gamelib"
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'wolpert'
                        name = 'Ned Wolpert'
                        email = 'ned.wolpert@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/wolpert/gamelib.git'
                    developerConnection = 'scm:git:ssh://github.com/wolpert/gamelib.git'
                    url = 'https://github.com/wolpert/gamelib/'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            name = "ossrh"
            credentials(PasswordCredentials)
        }
    }
}
signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}