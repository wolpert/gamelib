/*
 * Copyright (c) 2020-2021. CodeHead Systems. All rights reserved
 * Ned Wolpert <ned.wolpert@codeheadsystems.com>
 */

ext {
    appName = "gamelib-core"
}

base {
    archivesName = "${appName}"
    version = "${version}"
}

//jacocoTestReport.finalizedBy jacocoTestCoverageVerification

dependencies {
    api libs.gdx
    api libs.gdx.backend.lwjgl3

    // Dagger
    implementation libs.dagger
    annotationProcessor libs.dagger.compiler

    testImplementation libs.assertj.core
    testImplementation libs.junit.jupiter.engine
    testImplementation libs.junit.jupiter.api
    testImplementation libs.junit.jupiter.params
    testImplementation libs.mockito.junit.jupiter
    testImplementation libs.mockito.core
    testImplementation libs.gdx.backend.lwjgl3
    testImplementation libs.gdx.backend.headless
    testImplementation libs.commons.io
    testImplementation("com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop")
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = appName
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = appName
                description = 'Dagger support for libGDX for quick project setup.'
                url = "https://github.com/wolpert/gamelib"
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'wolpert'
                        name = 'Ned Wolpert'
                        email = 'ned.wolpert@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/wolpert/gamelib.git'
                    developerConnection = 'scm:git:ssh://github.com/wolpert/gamelib.git'
                    url = 'https://github.com/wolpert/gamelib/'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            name = "ossrh"
            credentials(PasswordCredentials)
        }
    }
}
signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}