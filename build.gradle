/*
 * Copyright (c) 2020-2021. CodeHead Systems. All rights reserved
 * Ned Wolpert <ned.wolpert@codeheadsystems.com>
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://maven.google.com" } // Google's Maven repository
        gradlePluginPortal() // was jcenter() which is dying
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath 'com.google.gms:google-services:4.3.3'
        classpath 'org.gretty:gretty:4.1.3'
    }
}

allprojects {
    group 'com.codeheadsystems'
    version '1.0.4-SNAPSHOT'
    apply plugin: "base"
    apply plugin: "jvm-test-suite"
    apply plugin: "java-library"
    apply plugin: "idea"
    apply plugin: 'jacoco'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'
    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
        withJavadocJar()
        withSourcesJar()
    }
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()  // was jcenter() which is dying
        google()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://s3-us-west-2.amazonaws.com/dynamodb-local/release/" }
    }
    dependencies {
        api libs.gdx

        // Dagger
        implementation libs.dagger
        annotationProcessor libs.dagger.compiler
    }
    test {
        finalizedBy jacocoTestReport
    }
    testing {
        suites {
            test {
                useJUnitJupiter()
                dependencies {
                    implementation libs.assertj
                    implementation libs.junit.jupiter.engine
                    implementation libs.junit.jupiter.api
                    implementation libs.junit.jupiter.params
                    implementation libs.mockito.junit.jupiter
                    implementation libs.mockito.core
                    implementation libs.gdx.backend.lwjgl3
                    implementation "com.badlogicgames.gdx:gdx-platform:1.12.1:natives-desktop"

                    implementation libs.gdx.backend.headless
                    implementation libs.commons.io
                    implementation libs.slf4j.log4j12
                    implementation libs.log4j.api
                    implementation libs.log4j.core
                    implementation libs.logback.classic
                    implementation libs.logback.core
                    implementation libs.dagger // Common files for runtime
                    annotationProcessor libs.dagger.compiler

                    implementation(libs.codehead.test)

                }
            }
        }
    }
    jacocoTestReport {
        dependsOn test
        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        '**/dagger/**.class',
                        '**/Dagger*Builder.class',
                        '**/*Exception.class',
                        '**/*Module.class',
                        '**/*_Factory.class',
                        '**/*_Factory.*class',
                        '**/*InstanceHolder.class',
                        '**/*_Factory$InstanceHolder.class'
                ])
            }))
        }
    }
    def excludeFiles = [
            '**.dagger.**',
            '**.Dagger*Builder',
            '**.*Exception',
            '**.*Module',
            '**.*_Factory',
            '**.*_Factory*',
            '**.InstanceHolder',
            '**.*_Factory$InstanceHolder'
    ]

// If you want to override, use this in your package and set. This is the low bar.
    jacocoTestCoverageVerification {
        dependsOn jacocoTestReport
        violationRules {
            rule {
                excludes = excludeFiles
                element = 'CLASS'
                limit {
                    counter = 'LINE'
                    value = 'COVEREDRATIO'
                    minimum = 0.8
                }
            }
            rule {
                excludes = excludeFiles
                element = 'CLASS'
                limit {
                    counter = 'BRANCH'
                    value = 'COVEREDRATIO'
                    minimum = 0.7
                }
            }
        }
    }

    javadoc {
        if(JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

}


